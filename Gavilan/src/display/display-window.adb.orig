--------------------------------------------------------------------------------
-- LIBRARY_UNIT_NAME : Display.Window
--
-- DESIGNER          : Guillermo HAZEBROUCK
--
-- CREATION_DATE     : 12 Aug 2020
--------------------------------------------------------------------------------
--Local
with Ada.Text_IO;
with Display.Menu;
with Glfw;
with Glfw.Windows;
with Glfw.Windows.Context;
with Glfw.Input;
with Glfw.Input.Mouse;
with Glfw.Input.Keys;
with Gl;
with Gl.Shaders;
with Gl.Fonts;
with Glfw.Input.Mouse;


--******************************************************************************
--
--******************************************************************************
package body Display.Window is

   Main_Window : aliased Glfw.Windows.Window;

   Alive : Boolean := False;

   Matrix : Gl.Gl_Mat_4 := Gl.Gl_Mat_4_Identity;

   procedure Mouse_Button_Changed (Button  : Glfw.Input.Mouse.Button;
                                   State   : Glfw.Input.Button_State;
                                   Mods    : Glfw.Input.Keys.Modifiers);

   --===========================================================================
   --
   --===========================================================================
   function Is_Alive return Boolean is
   begin

      return Alive;

   end Is_Alive;
   -----------------------------------------------------------------------------



   --===========================================================================
   --
   --===========================================================================
   procedure Init is
   begin

      Glfw.Init;

      Glfw.Windows.Init (Main_Window'Access, 305, 400, "Gavilan");

      Ada.Text_IO.Put_Line ("GLFW version: " & Glfw.Version_String);

      Main_Window.Set_Mouse_Button_Changed (Mouse_Button_Changed'Access);

      Gl.Set_Mode (Gl.Core_Mode);

      Gl.Shaders.Init (Gl.Shaders.Glsl_330, 1);

      Gl.Fonts.Init;

      Display.Menu.Init;

      Gl.Translate (Matrix, -1.0, -1.0, 0.0);

      Gl.Scale     (Matrix,  2.0,  2.0, 0.0);

      Alive := True;

      Refresh := True;

   end Init;
   -----------------------------------------------------------------------------




   --===========================================================================
   --
   --===========================================================================
   procedure Enter_Main_Loop is

      use Gl;
      use type Glfw.Seconds;

      H, W : Glfw.Size;

   begin

      while not Main_Window.Should_Close loop

         if Refresh then

            -- Setup OpenGL
            -----------------------------------------------------

            Main_Window.Get_Size (W, H);

            Gl.Enable      (GL_BLEND);
            Gl.Blend_Func  (GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA);
            Gl.Clear_Color (0.2, 0.5, 1.0, 1.0);
            Gl.Clear       (GL_COLOR_BUFFER_BIT + GL_DEPTH_BUFFER_BIT);
            Gl.Viewport    (0, 0, W, H);

            Gl.Shaders.Bind_Shader (Gl.Shaders.Lines_2D, True);

            Gl.Shaders.Load_Matrix (Matrix);

            Gl.Shaders.Load_Aspect (0.5 * Gl_Float (W),
                                    0.5 * Gl_Float (H));

            -- Draw display
            -----------------------------------------------------

            Display.Menu.Draw (Float (W), Float (H));

            Glfw.Windows.Context.Swap_Buffers (Main_Window'Access);

            Refresh := False;

         end if;

         if Glfw.Time >= 0.5 then

            Glfw.Set_Time (0.0);

            -- Do cyclic stuff
            -----------------------------------------------------

            Refresh := True;

         end if;

         Glfw.Input.Poll_Events;

         delay 0.001;

      end loop;

      Glfw.Shutdown;

      Alive := False;

   end Enter_Main_Loop;
   -----------------------------------------------------------------------------




   --===========================================================================
   --
   --===========================================================================
   procedure Mouse_Button_Changed (Button  : Glfw.Input.Mouse.Button;
                                   State   : Glfw.Input.Button_State;
                                   Mods    : Glfw.Input.Keys.Modifiers) is

      use Glfw.Input;
      use Glfw.Input.Mouse;

      H, W             : Glfw.Size;
      Mouse_X, Mouse_Y : Glfw.Input.Mouse.Coordinate;
      X, Y             : Float;

   begin

      if
        Button = Left_Button and
        State  = Pressed
      then

         Main_Window.Get_Cursor_Pos (Mouse_X, Mouse_Y);

         Main_Window.Get_Size (W, H);

         X :=       Float (Mouse_X) / Float (W);
         Y := 1.0 - Float (Mouse_Y) / Float (H);

         Display.Menu.Press (X, Y);

      end if;

   end Mouse_Button_Changed;
   -----------------------------------------------------------------------------

end Display.Window;
--------------------------------------------------------------------------------
